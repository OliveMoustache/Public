/*
//  Mighty World switch
// by Olivier Ladeuix
//

// This script is to switch a control from local orient to world orient 
// through baking. This script was tested successfully on Rosie's head.

/////////// Requirements //////////////////////
// 1. You need to have the full frame range viible
// 2. Select the control you want to mighty switch first. Only one at a time
////////////////////////////////

CHANGELOG:
2021 06 22    v1.0 
2021 06 23    v1.1    Now a procedure rather than a simple script 

////////////// How to use /////////////////////////
Place the script in your scripts folder
Do a rehash or restart Maya for Maya to load the script
Select the object you want to switch to from local to world
Trigger the script from the shelf or command line with:
    mighty_World_switch()
You could also use it as a Hotkey
////////////////////////////////////////////////////
    

*/




global proc mighty_World_switch() {    


// Make a variable from the selection 
string $mighty_World_switch_selection[] = `ls -sl`;
print ("Object selected is : " + $mighty_World_switch_selection[0]);

// Get the frame range and go to the start of the range
float $minTime = `playbackOptions -q -minTime`;
float $maxTime = `playbackOptions -q -maxTime`;
string $bakeRange = $minTime + ":" + $maxTime;
currentTime $minTime ;

// We could use this in the future to generate a shorter name
// referenceQuery -namespace `ls -sl`;

// Creation of the locator
spaceLocator -name mighty_World_switch_TEMP_locator ;

setAttr "mighty_World_switch_TEMP_locator.scaleX" 20 ;
setAttr "mighty_World_switch_TEMP_locator.scaleY" 20 ;
setAttr "mighty_World_switch_TEMP_locator.scaleZ" 20 ;

// Constraint the locator to the object selection for locator baking
parentConstraint -name mighty_constraint_TEMP_parentConstraint $mighty_World_switch_selection[0] mighty_World_switch_TEMP_locator  ;

// Bake the locator and delete the constraint
bakeResults -simulation true -t ($bakeRange) -sampleBy 1 -oversamplingRate 1 -disableImplicitControl 
true -preserveOutsideKeys true -sparseAnimCurveBake false -removeBakedAttributeFromLayer false -removeBakedAnimFromLayer 
false -bakeOnOverrideLayer false -minimizeRotation true -controlPoints false -shape true;

print "\n////////// Locator baked to Driver //////////";

delete mighty_constraint_TEMP_parentConstraint;

// Constraint the object to the locator for object baking
parentConstraint -name mighty_constraint_TEMP_parentConstraint_B mighty_World_switch_TEMP_locator  $mighty_World_switch_selection[0];
select $mighty_World_switch_selection[0];

// Switch the global attribute to Global (World orient)
cutKey -t ":" -f ":" -at "Global" $mighty_World_switch_selection[0];
setAttr ($mighty_World_switch_selection[0] + ".Global") 10 ;
setKeyframe ($mighty_World_switch_selection[0] + ".Global");
// setKeyframe -breakdown 0 -preserveCurveShape 0 -hierarchy none  -at "blendParent1" Rosie:FKHead_M_CTRL;
bakeResults -simulation true -t ($bakeRange) -sampleBy 1 -oversamplingRate 1 -disableImplicitControl true -preserveOutsideKeys
 true -sparseAnimCurveBake false -removeBakedAttributeFromLayer false -removeBakedAnimFromLayer false -bakeOnOverrideLayer false
  -minimizeRotation true -controlPoints false -shape true ;

print "\n////////// Driver baked to locator //////////";

delete mighty_constraint_TEMP_parentConstraint_B;
delete mighty_World_switch_TEMP_locator ;

print "\n////////// Mighty World switch completed //////////";


};
test

